import pandas as pd
import os.path as path

# ==============================================================================
# Configuration
# ==============================================================================
CONFIG = pd.read_csv("config.tsv", sep="\t", index_col=False)

REPORT_DIR = "Reports"
FASTQ_DIR = "FASTQs"
ALIGN_DIR = "Aligned"
CONTACT_DIR = "Contacts"

SAMPLES = CONFIG["Accession"].tolist()
READS = [1, 2]
BWT2_IDX = "/mnt/work1/data/genomes/human/hg38/iGenomes/Sequence/Bowtie2Index/genome"
CHRS = ["chr" + str(i) for i in list(range(1, 23)) + ["X", "Y"]]

RESOLUTIONS = [
    2000, 3000, 4000, 5000,
    10000, 20000, 30000, 40000, 50000,
    100000, 200000, 300000, 400000, 500000,
    1000000, 2000000, 3000000, 4000000, 5000000
]

wildcard_constraints:
    sample = "[A-Za-z0-9-]+",
    lane = "[1-4]",
    read = "[1-2]"


# ==============================================================================
# Meta Rules
# ==============================================================================
rule all:
    input:
        expand(
            path.join(REPORT_DIR, "{sample}_R{read}_fastqc.zip"),
            sample=SAMPLES,
            read=READS
        ),


# ==============================================================================
# Rules
# ==============================================================================
# Split SRR FASTQs into read mates
# -----------------------------------------------------------------------------------------------------------------------------
rule split_fq_R1:
    input:
        path.join(FASTQ_DIR, "{sample}.fastq.gz"),
    output:
        path.join(FASTQ_DIR, "{sample}_R1.fastq.gz"),
    shell:
        "zcat {input} | awk '{{switch (NR % 4){{case 1: print $1; break; case 2: print substr($1, 1, 76); break; case 3: print \"+\"; break; case 0: print substr($1, 1, 76)}} }}' | gzip -nc > {output}"

rule split_fq_R2:
    input:
        path.join(FASTQ_DIR, "{sample}.fastq.gz"),
    output:
        path.join(FASTQ_DIR, "{sample}_R2.fastq.gz"),
    shell:
        "zcat {input} | awk '{{switch (NR % 4){{case 1: print $1; break; case 2: print substr($1, 77, 76); break; case 3: print \"+\"; break; case 0: print substr($1, 77, 76)}} }}' | gzip -nc > {output}"

# Summaries
# -----------------------------------------------------------------------------------------------------------------------------
rule fastqc:
    input:
        path.join(FASTQ_DIR, "{sample}_R{read}.fastq.gz"),
    output:
        path.join(REPORT_DIR, "{sample}_R{read}_fastqc.html"),
        path.join(REPORT_DIR, "{sample}_R{read}_fastqc.zip"),
    shell:
        "fastqc {input} -o {REPORT_DIR}"

