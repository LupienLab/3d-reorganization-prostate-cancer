import pandas as pd
import numpy as np
import os.path as path

# ==============================================================================
# Configuration
# ==============================================================================
CONFIG = pd.read_csv('config.tsv', index_col=False, sep='\t')

FASTQ_DIR = 'FASTQs'
ALIGN_DIR = 'Aligned'
PEAK_DIR = 'Peaks'
REPORT_DIR = 'Reports'


# SAMPLES = [19121, 33173] # only using a subset of all samples
SAMPLES = CONFIG['Prostate ID'].tolist()
CHIPS = ['input', 'H3K27ac']

GENOMES = ['hg19', 'hg38']
BWT2_IDX = {
    'hg19': '/mnt/work1/data/genomes/human/hg19/iGenomes/Sequence/Bowtie2Index/genome',
    'hg38': '/mnt/work1/data/genomes/human/hg38/iGenomes/Sequence/Bowtie2Index/genome',
}


# ==============================================================================
# Meta Rules
# ==============================================================================
rule all:
    input:
        expand(
            path.join(REPORT_DIR, 'Pca{sample}_{chip}_R1_fastqc.html'),
            sample=SAMPLES,
            chip=CHIPS
        ),
        expand(
            path.join(ALIGN_DIR, '{genome}', 'Pca{sample}_{chip}.sorted.dedup.bam'),
            genome=GENOMES,
            sample=SAMPLES,
            chip=CHIPS
        ),

# ==============================================================================
# Rules
# ==============================================================================
rule fastqc:
    input:
        path.join(FASTQ_DIR, 'Sample_Pca_{sample}_{chip}',
                  'Pca{sample}_{chip}_R1.fastq.gz')
    output:
        path.join(
            REPORT_DIR, 'Pca{sample}_{chip}_R1_fastqc.html')
    params:
        '-o {}'.format(REPORT_DIR)
    conda:
        '/mnt/work1/users/home2/hawleyj/Source/Terminal/Anaconda/ChIPseq.yaml'
    shell:
        'fastqc {params} {input}'

# Alignment
# ------------------------------------------------------------------------------
rule align_hg38:
    input:
        path.join(FASTQ_DIR, 'Sample_Pca_{sample}_{chip}',
                  'Pca{sample}_{chip}_R1.fastq.gz')
    output:
        bam = protected(path.join(ALIGN_DIR, 'hg38', 'Pca{sample}_{chip}.bam')),
        rpt = path.join(
            REPORT_DIR, 'hg38', 'Pca{sample}_{chip}.alignment_report.txt')
    params:
        '-x {}'.format(BWT2_IDX['hg38'])
    conda:
        '/mnt/work1/users/home2/hawleyj/Source/Terminal/Anaconda/ChIPseq.yaml'
    shell:
        'bowtie2 {params} -U {input} 2> {output.rpt} | samtools view -u > {output.bam}'

rule align_hg19:
    input:
        path.join(FASTQ_DIR, 'Sample_Pca_{sample}_{chip}',
                  'Pca{sample}_{chip}_R1.fastq.gz')
    output:
        bam = protected(path.join(ALIGN_DIR, 'hg19', 'Pca{sample}_{chip}.bam')),
        rpt = path.join(
            REPORT_DIR, 'hg19', 'Pca{sample}_{chip}.alignment_report.txt')
    params:
        '-x {}'.format(BWT2_IDX['hg19'])
    conda:
        '/mnt/work1/users/home2/hawleyj/Source/Terminal/Anaconda/ChIPseq.yaml'
    shell:
        'bowtie2 {params} -U {input} 2> {output.rpt} | samtools view -u > {output.bam}'


# Deduplicate
# ------------------------------------------------------------------------------
rule dedup:
    input:
        '{file}.bam'
    output:
        '{file}.dedup.bam'
    conda:
        '/mnt/work1/users/home2/hawleyj/Source/Terminal/Anaconda/ChIPseq.yaml'
    shell:
        'sambamba markdup -r -p {input} {output}'

# Sort
# ------------------------------------------------------------------------------
rule sort:
    input:
        '{file}.bam'
    output:
        '{file}.sorted.bam',
        '{file}.sorted.bam.bai'
    conda:
        '/mnt/work1/users/home2/hawleyj/Source/Terminal/Anaconda/ChIPseq.yaml'
    shell:
        'sambamba sort {input}'
