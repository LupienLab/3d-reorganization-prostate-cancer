import pandas as pd
import numpy as np
import os.path as path

# ==============================================================================
# Configuration
# ==============================================================================
CONFIG = pd.read_csv('config.tsv', index_col=False, sep='\t')

FASTQ_DIR = 'FASTQs'
ALIGN_DIR = 'Aligned'
PEAK_DIR = 'Peaks'
REPORT_DIR = 'Reports'

SAMPLES = CONFIG['Prostate ID'].tolist()

GENOMES = ['hg19', 'hg38']
BWT2_IDX = {
    'hg19': '/mnt/work1/data/genomes/human/hg19/iGenomes/Sequence/Bowtie2Index/genome',
    'hg38': '/mnt/work1/data/genomes/human/hg38/iGenomes/Sequence/Bowtie2Index/genome',
}


# ==============================================================================
# Meta Rules
# ==============================================================================
rule all:
    input:
        expand(
            path.join(REPORT_DIR, 'Pca{sample}_H3K27ac_R1_fastqc.html'),
            sample=SAMPLES
        ),
        expand(
            path.join(ALIGN_DIR, 'hg38', 'Pca{sample}_H3K27ac.sorted.dedup.bam'),
            sample=SAMPLES
        ),

# ==============================================================================
# Rules
# ==============================================================================
rule fastqc:
    input:
        path.join(FASTQ_DIR, 'Sample_Pca_{sample}_H3K27ac',
                  'Pca{sample}_H3K27ac_R1.fastq.gz')
    output:
        path.join(REPORT_DIR, 'Pca{sample}_H3K27ac_R1_fastqc.html'),
        path.join(REPORT_DIR, 'Pca{sample}_H3K27ac_R1_fastqc.zip')
    params:
        '-o {}'.format(REPORT_DIR)
    shell:
        'fastqc {params} {input}'

# Alignment
# ------------------------------------------------------------------------------
rule align_hg38:
    input:
        path.join(FASTQ_DIR, 'Sample_Pca_{sample}_H3K27ac',
                  'Pca{sample}_H3K27ac_R1.fastq.gz')
    output:
        bam = protected(path.join(ALIGN_DIR, 'hg38', 'Pca{sample}_H3K27ac.bam')),
        rpt = path.join(
            REPORT_DIR, 'hg38', 'Pca{sample}_H3K27ac.alignment_report.txt')
    params:
        # have to trim these reads to 52 bp to match the inputs on the other flow cell
        # only doing single-end alignment to match other flow cell, not paired-end
        '-x {} -3 50'.format(BWT2_IDX['hg38'])
    shell:
        'bowtie2 {params} -U {input} 2> {output.rpt} | samtools view -u > {output.bam}'

rule align_hg19:
    input:
        path.join(FASTQ_DIR, 'Sample_Pca_{sample}_H3K27ac',
                  'Pca{sample}_H3K27ac_R1.fastq.gz')
    output:
        bam = protected(path.join(ALIGN_DIR, 'hg19', 'Pca{sample}_H3K27ac.bam')),
        rpt = path.join(
            REPORT_DIR, 'hg19', 'Pca{sample}_H3K27ac.alignment_report.txt')
    params:
        # have to trim these reads to 52 bp to match the inputs on the other flow cell
        # only doing single-end alignment to match other flow cell, not paired-end
        '-x {} -3 50'.format(BWT2_IDX['hg19'])
    shell:
        'bowtie2 {params} -U {input} 2> {output.rpt} | samtools view -u > {output.bam}'


# Deduplicate
# ------------------------------------------------------------------------------
rule dedup:
    input:
        '{file}.bam'
    output:
        '{file}.dedup.bam'
    shell:
        'sambamba markdup -r -p {input} {output}'

# Sort
# ------------------------------------------------------------------------------
rule sort:
    input:
        '{file}.bam'
    output:
        '{file}.sorted.bam',
        '{file}.sorted.bam.bai'
    shell:
        'sambamba sort {input}'
