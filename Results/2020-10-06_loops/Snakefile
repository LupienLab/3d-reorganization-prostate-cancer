# ==============================================================================
# Configuration
# ==============================================================================
import pandas as pd
import os.path as path

CONFIG = pd.read_csv("config.tsv", index_col=False, sep="\t")
CONFIG = CONFIG.loc[CONFIG.Include == "Yes", :]

SAMPLES = CONFIG["SampleID"].tolist()

LOOP_DIR = path.join("..", "..", "Data", "Processed", "2019-06-18_PCa-LowC-sequencing", "Loops")
PROC_DIR = "Loops"
TRACK_DIR = "Tracks"
PLOT_DIR = "Plots"

# ==============================================================================
# Meta Rules
# ==============================================================================
rule all:
	input:
		# aggregate and count loops and anchors across the cohort
		path.join(PROC_DIR, "merged-loops.tsv"),
		path.join(PROC_DIR, "merged-loops.sample-counts.tsv"),
		path.join(PROC_DIR, "shared-loops.tsv"),
		path.join(PROC_DIR, "benign-specific-loops.tsv"),
		path.join(PROC_DIR, "tumour-specific-loops.tsv"),
		# sample-specific loop anchors and tracks
		expand(
			path.join(PROC_DIR, "by-sample", "{sample}.anchors.bed"),
			sample=SAMPLES,
		),
		expand(
			path.join(TRACK_DIR, "{sample}.loops.bed2ddb"),
			sample=SAMPLES,
		),
		expand(
			path.join(TRACK_DIR, "{sample}.anchors.beddb"),
			sample=SAMPLES,
		),
		path.join(TRACK_DIR, "merged-loops.sample-counts.bed2ddb"),
		path.join(TRACK_DIR, "tumour-specific-loops.bed2ddb"),
		path.join(TRACK_DIR, "benign-specific-loops.bed2ddb"),
		path.join(TRACK_DIR, "shared-loops.bed2ddb"),
		# loop saturation analysis
		path.join(PROC_DIR, "loop-saturation.model-estimates.tsv"),
		path.join(PROC_DIR, "loop-saturation.saturation-estimates.tsv"),
		path.join(PROC_DIR, "loop-saturation.bootstraps.tsv"),
		path.join(PROC_DIR, "loop-saturation.bootstrap-summary.tsv"),
		path.join(PLOT_DIR, "loop-saturation.All.png"),
		# plots and stats for loop calls
		path.join(PLOT_DIR, "loop-calls.per-sample.png"),
		path.join(PLOT_DIR, "loop-calls.per-sample.pdf"),
		path.join(PLOT_DIR, "loop-calls.anchor-distance.png"),
		path.join(PLOT_DIR, "apa.pileup.samples.png"),
		path.join(PLOT_DIR, "apa.pileup.conditional.png"),
		path.join(PLOT_DIR, "apa.differential.png"),
		path.join(PLOT_DIR, "apa.differential.ranked.png"),
		path.join(PLOT_DIR, "apa.specific-loops.png"),
		path.join(PLOT_DIR, "apa.specific-loops.pdf"),


# ==============================================================================
# Rules
# ==============================================================================
rule anchors:
	input:
		script = "get-anchors.R",
		data = expand(
			path.join(LOOP_DIR, "{sample}.res_10000bp.loops.tsv"),
			sample=SAMPLES,
		),
	output:
		path.join(PROC_DIR, "merged-loops.tsv"),
		path.join(PROC_DIR, "merged-loops.sample-counts.tsv"),
		path.join(PROC_DIR, "shared-loops.tsv"),
		path.join(PROC_DIR, "benign-specific-loops.tsv"),
		path.join(PROC_DIR, "tumour-specific-loops.tsv"),
		expand(
			path.join(PROC_DIR, "by-sample", "{sample}.loops.bedpe"),
			sample=SAMPLES,
		),
		expand(
			path.join(PROC_DIR, "by-sample", "{sample}.anchors.bed"),
			sample=SAMPLES,
		),
	shell:
		"Rscript {input.script}"

rule loop_saturation:
	input:
		script = "loop-saturation.R",
		loops = path.join(PROC_DIR, "merged-loops.tsv"),
	output:
		path.join(PROC_DIR, "loop-saturation.model-estimates.tsv"),
		path.join(PROC_DIR, "loop-saturation.saturation-estimates.tsv"),
		path.join(PROC_DIR, "loop-saturation.bootstraps.tsv"),
		path.join(PROC_DIR, "loop-saturation.bootstrap-summary.tsv"),
		path.join(PLOT_DIR, "loop-saturation.All.png"),
	shell:
		"Rscript {input.script}"

rule plot_loops:
	input:
		script = "plot-loops.R",
		all_loops = path.join(PROC_DIR, "merged-loops.tsv"),
		counted_loops = path.join(PROC_DIR, "merged-loops.sample-counts.tsv"),
	output:
		path.join(PLOT_DIR, "loop-calls.per-sample.png"),
		path.join(PLOT_DIR, "loop-calls.per-sample.pdf"),
		path.join(PLOT_DIR, "loop-calls.anchor-distance.png"),
	shell:
		"Rscript {input.script}"

rule clodius_2d_tsv:
	input:
		path.join(PROC_DIR, "{file}.tsv"),
	output:
		path.join(TRACK_DIR, "{file}.bed2ddb"),
	params:
		"--assembly hg38 --has-header --chr1-col 1 --from1-col 2 --to1-col 3 --chr2-col 4 --from2-col 5 --to2-col 6"
	shell:
		"clodius aggregate bedpe {params} -o {output} {input}"

rule clodius_2d_anchors:
	input:
		path.join(PROC_DIR, "by-sample", "{file}.anchors.bed"),
	output:
		path.join(TRACK_DIR, "{file}.anchors.beddb"),
	params:
		"--assembly hg38"
	shell:
		"clodius aggregate bedfile {params} -o {output} {input}"

rule clodius_2d_loops:
	input:
		path.join(PROC_DIR, "by-sample", "{file}.loops.bedpe"),
	output:
		path.join(TRACK_DIR, "{file}.loops.bed2ddb"),
	params:
		"--assembly hg38 --chr1-col 1 --from1-col 2 --to1-col 3 --chr2-col 4 --from2-col 5 --to2-col 6"
	shell:
		"clodius aggregate bedpe {params} -o {output} {input}"

rule apa:
	input:
		script = "apa.py",
		loops = path.join(PROC_DIR, "merged-loops.sample-counts.tsv"),
	output:
		path.join(PROC_DIR, "snipper.obj"),
		path.join(PROC_DIR, "stack.obj"),
		path.join(PROC_DIR, "stack.conditional.obj"),
		path.join(PROC_DIR, "stack.conditional.differential.obj"),
		path.join(PROC_DIR, "pileup.obj"),
		path.join(PROC_DIR, "pileup.conditional.obj"),
		path.join(PROC_DIR, "differential.obj"),
	shell:
		"python {input.script}"

rule plot_apa:
	input:
		script = "plot-apa.py",
		loops = path.join(PROC_DIR, "merged-loops.sample-counts.tsv"),
		pileup = path.join(PROC_DIR, "pileup.obj"),
		cdn_pile = path.join(PROC_DIR, "pileup.conditional.obj"),
		cdn_stack = path.join(PROC_DIR, "stack.conditional.obj"),
		diff = path.join(PROC_DIR, "differential.obj"),
	output:
		path.join(PLOT_DIR, "apa.pileup.samples.png"),
		path.join(PLOT_DIR, "apa.pileup.conditional.png"),
		path.join(PLOT_DIR, "apa.differential.png"),
		path.join(PLOT_DIR, "apa.differential.ranked.png"),
		path.join(PLOT_DIR, "apa.specific-loops.png"),
		path.join(PLOT_DIR, "apa.specific-loops.pdf"),
	shell:
		"python {input.script}"
