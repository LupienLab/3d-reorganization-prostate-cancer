import pandas as pd
import os.path as path

# ==============================================================================
# Configuration
# ==============================================================================
CONFIG = pd.read_csv(
    path.join('..', '..', 'Data', 'External',
              'LowC_Samples_Data_Available.tsv'),
    sep='\t',
    index_col=False
)
SAMPLES = ['PCa' + str(i) for i in CONFIG['Sample ID']]

BAM_DIR = path.join(
    '..', '..', 'Data', 'Processed', '2019-06-18_PCa-LowC-sequencing', 'Aligned'
)
BREAK_DIR = 'Breakpoints'
SCRIPT_DIR = 'Scripts'
PLOT_DIR = 'Plots'
REPORT_DIR = 'Reports'

# ==============================================================================
# Meta Rules
# ==============================================================================
rule all:
    input:
        expand(
            path.join(BREAK_DIR, '{param}', '{sample}.breaks.sorted.bedpe'),
            sample=SAMPLES,
            param=['Default', 'Min_1kbp', 'Low_Thresh_30']
        ),
        expand(
            path.join(BREAK_DIR, "Default", "{sample}.breaks.sorted.manually-resolved.breakpoints.sorted.bed"),
            sample=SAMPLES
        ),
        expand(
            path.join(BREAK_DIR, "Default", "{sample}.breaks.bed2ddb"),
            sample=SAMPLES
        ),
        path.join(BREAK_DIR, "Default", "inter-intra-chromosomal-events.tsv"),
        "translocation-disrupted-genes.tsv",
        path.join(PLOT_DIR, "translocation-disrupted-genes.png"),

# ==============================================================================
# Rules
# ==============================================================================
rule install_breakfinder_dev:
    input:
        "hic_breakfinder/configure"
    output:
        "hic_breakfinder/bin/hic_breakfinder"
    params:
        'CPPFLAGS="-I${{CONDA_PREFIX}}/include/bamtools/ -I${{CONDA_PREFIX}}/include/eigen3/"',
        'LDFLAGS="-L${{CONDA_PREFIX}}/lib/"',
        '--prefix="."',
        '--host=linux'
    run:
        commands = [
            'pushd hic_breakfinder/',
            './configure {params}',
            'make',
            'make install'
        ]
        command_str = '; '.join(commands)
        shell(command_str)

rule breakfinder_default:
    input:
        bam = path.join(BAM_DIR, '{sample}.agg.hicup.sorted.bam'),
        inter = 'inter_expect_1Mb.hg38.txt',
        intra = 'intra_expect_100kb.hg38.txt'
    output:
        path.join(BREAK_DIR, 'Default', '{sample}.breaks.txt'),
    shell:
        'pushd {BREAK_DIR}/Default; hic_breakfinder --bam-file {input.bam} --exp-file-inter ../../{input.inter} --exp-file-intra ../../{input.intra} --name {wildcards.sample}; popd;'

rule breakfinder_1kb:
    input:
        bam = path.join(BAM_DIR, '{sample}.agg.hicup.sorted.bam'),
        inter = 'inter_expect_1Mb.hg38.txt',
        intra = 'intra_expect_100kb.hg38.txt'
    output:
        path.join(BREAK_DIR, 'Min_1kb', '{sample}.breaks.txt'),
    shell:
        'pushd {BREAK_DIR}/Min_1kbp; hic_breakfinder --bam-file {input.bam} --exp-file-inter ../../{input.inter} --exp-file-intra ../../{input.intra} --name {wildcards.sample} --min-1kb; popd;'

rule breakfinder_low_thresh:
    input:
        bam = path.join(BAM_DIR, '{sample}.agg.hicup.sorted.bam'),
        inter = 'inter_expect_1Mb.hg38.txt',
        intra = 'intra_expect_100kb.hg38.txt'
    output:
        path.join(BREAK_DIR, 'Low_Thresh_{thresh}', '{sample}.breaks.txt'),
    params:
        threshold = lambda wildcards: '--thresh {}'.format(wildcards.thresh)
    run:
        commands = [
            'pushd {BREAK_DIR}/Low_Thresh_{wildcards.thresh}',
            '../../hic_breakfinder/bin/hic_breakfinder --name {wildcards.sample} {params.threshold} --bam-file {input.bam} --exp-file-inter ../../{input.inter} --exp-file-intra ../../{input.intra}',
            'popd'
        ]
        command_str = '; '.join(commands)
        shell(command_str)

rule sort_breaks:
    input:
        path.join(BREAK_DIR, "{prefix}", "{sample}.breaks.txt")
    output:
        path.join(BREAK_DIR, "{prefix}", "{sample}.breaks.sorted.bedpe")
    shell:
        "awk '{{FS=OFS=\"\\t\"}}{{print $2, $3, $4, $6, $7, $8, \".\", $1, $5, $9, $10}}' {input} | sort -k1,1 -V -k4,4 -V -k2,2n -k5,5n > {output}"

rule agg_breaks:
    input:
        script = path.join(SCRIPT_DIR, 'agg-breaks.R'),
        data = expand(
            path.join(BREAK_DIR, '{{calltype}}', '{sample}.breaks.txt'),
            sample=SAMPLES
        )
    output:
        path.join(BREAK_DIR, '{calltype}', 'breakpoints.bedpe'),
        path.join(BREAK_DIR, '{calltype}', 'breakpoints.tsv'),
    shell:
        'Rscript {input.script} {input.data} -p {BREAK_DIR}/{wildcards.calltype}/breakpoints'

rule plot_breakpoints:
    input:
        wrapper = path.join(SCRIPT_DIR, 'plot-breakpoint-matrices.sh'),
        script = path.join('..', '2019-07-08_TADs', 'plot-tads.py'),
        data = path.join(BREAK_DIR, 'Default', '{sample}.breakspoints.bedpe')
    output:
        path.join(REPORT_DIR, 'breakpoint-plots.txt')
    shell:
        'bash {input.wrapper} -i {input.data} -p {PLOT_DIR}/Breakpoints/{wildcards.sample} > {output}'

rule bf2vcf:
    input:
        script = path.join(SCRIPT_DIR, "bf2vcf.py"),
        data = path.join(BREAK_DIR, "Default", "{sample}.breaks.txt")
    output:
        path.join(BREAK_DIR, "Default", "{sample}.vcf")
    shell:
        'python {input.script} {input.data} -o {output}'

rule sort_vcf:
    input:
        "{file}.vcf"
    output:
        "{file}.sorted.vcf"
    shell:
        "awk '/^#/ {{print $0; next}} {{print $0 | \"sort -k1,1 -V -k2,2n\"}}' {input} > {output}"

rule sort_bed:
    input:
        "{file}.bed",
    output:
        "{file}.sorted.bed",
    shell:
        "sort -k1,1 -V -k2,2n {input} > {output}"

rule bf2bed:
    input:
        path.join(BREAK_DIR, "Default", "{sample}.breaks.sorted.manually-resolved.tsv")
    output:
        path.join(BREAK_DIR, "Default", "{sample}.breaks.sorted.manually-resolved.breakpoints.bed")
    params:
        '-v FS="\t" -v OFS="\t"'
    shell:
        'awk {params} \'{{if ($12 != \"ARTEFACT\") print $1, $2, $3, "SV"NR, "\\n"$4, $5, $6, "SV"NR}}\' {input} > {output}'

rule t2e_translocation_genes:
    input:
        gencode = path.join("..", "..", "Data", "External",
                            "GENCODE", "gencode.v33.genes.sorted.bed"),
        genome = path.join("..", "..", "Data", "Processed",
                           "2019-06-18_PCa-LowC-sequencing", "hg38.sizes.txt"),
    output:
        "translocation-disrupted-genes.tsv"
    shell:
        "bedtools intersect -sorted -g {input.genome} -a {input.gencode} -b <(echo -e \"chr14\\t34560000\\t35840000\nchr21\\t35440000\\t41500000\") > {output}"

rule plot_t2e_translocation_genes:
    input:
        script = path.join(SCRIPT_DIR, "plot-translocation-disrupted-genes.R"),
        data = "translocation-disrupted-genes.tsv",
    output:
        path.join(PLOT_DIR, "translocation-disrupted-genes.png"),
    shell:
        "Rscript {input.script}"

rule tally_inter_intra_events:
    input:
        expand(
            path.join(BREAK_DIR, "Default", "{sample}.breaks.sorted.manually-resolved.tsv"),
            sample=SAMPLES
        )
    output:
        path.join(BREAK_DIR, "Default", "inter-intra-chromosomal-events.tsv"),
    shell:
        """echo -e "Sample\tIntra-chromosomal\tInter-chromosomal" > {output};
        for s in PCa{{13266,13848,14121,19121,3023,33173,40507,51852,53687,56413,57054,57294,58215}}; do
            awk -v s=$s  -v FS="\\t" -v OFS="\\t" 'BEGIN{{x=0; y=0;}}{{if ($12 != "ARTEFACT") {{if ($1 == $4) {{x+=1}} else {{y += 1}} }} }}END{{print s, x, y}}' {BREAK_DIR}/Default/$s.breaks.sorted.manually-resolved.tsv >> {output}
        done
        """

rule clodius_2d:
    input:
        path.join(BREAK_DIR, "Default", "{sample}.breaks.sorted.bedpe"),
    output:
        path.join(BREAK_DIR, "Default", "{sample}.breaks.bed2ddb"),
    params:
        "--chr1-col 1 --from1-col 2 --to1-col 3 --chr2-col 4 --from2-col 5 --to2-col 6"
    shell:
        "clodius aggregate bedpe {params} -o {output} {input}"

